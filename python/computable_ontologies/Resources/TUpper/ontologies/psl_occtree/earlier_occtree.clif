/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The arboreal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/arborealcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/

(cl-text http://colore.oor.net/psl_occtree/earlier_occtree.clif

(cl-comment 'The earlier relation is restricted to 
arboreal activity occurrences (that is, activity occurrences that
are elements of the occurrence tree).')

(forall (s1 s2)
	(if	(earlier s1 s2)
		(and	(arboreal s1)
			(arboreal s2))))

(cl-comment 'The ordering relation over occurrences is irreflexive.')

(forall (s1 s2)
	(if	(earlier s1 s2)
		(not (earlier s2 s1))))

(cl-comment 'The ordering relation over occurrences is transitive.')

(forall (s1 s2 s3)
	(if	(and	(earlier s1 s2)
			(earlier s2 s3))
		(earlier s1 s3)))

(cl-comment 'A branch in the occurrence tree is totally ordered.')

(forall (s1 s2 s3)
	(if	(and	(earlier s1 s2)
			(earlier s3 s2))
		(or	(earlier s1 s3)
			(earlier s3 s1)
			(= s3 s1))))

(cl-comment 'The occurrence tree is infinite.')

(forall (s1)
	(if	(arboreal s1)
		(exists (s2)
			(earlier s1 s2))))

(cl-comment 'Every branch of the arboreal occurrence tree has an initial occurrence.')

(forall (s1 s2)
	(if	(earlier s1 s2)
		(exists (s3)
			(and	(initial s3)
				(or	(earlier s3 s1)
					(= s3 s1))))))

(cl-comment 'The earlier relation is a discrete ordering, so every
activity has an upwards successor in the ordering.')

(forall (s1 s2)
        (if     (and    (earlier s1 s2)
                        (not (= s1 s2)))
                (exists (s3)
                        (and    (earlier s1 s3)
                                (earlier s3 s2)
                                (not (= s3 s1))
                                (forall (s4)
                                        (if     (and    (earlier s1 s4)
                                                        (earlier s4 s3))
                                                (or     (= s4 s1)
                                                        (= s4 s3))))))))

(cl-comment 'The earlier relation is a discrete ordering, so every
activity has a downwards successor in the ordering.')

(forall (s1 s2) 
        (if     (and    (earlier s1 s2)
                        (not (= s1 s2)))
                (exists (s3)
                        (and    (earlier s1 s3)
                                (earlier s3 s2)
                                (not (= s3 s2))
                                (forall (s4)
                                        (if     (and    (earlier s3 s4)
                                                        (earlier s4 s2))
                                                (or     (= s4 s2)
                                                        (= s4 s3))))))))

(forall (o1)
	(iff	(initial o1)
		(and	(arboreal o1)
			(not (exists (o2)
				(earlier o2 o1))))))

)
