/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text https://raw.githubusercontent.com/mereolog/computational_ontologies/main/tupper/multidim_mereotopology_codi/theorems/codi_down_sum_theorems.clif

(cl-imports https://raw.githubusercontent.com/mereolog/computational_ontologies/main/tupper/multidim_mereotopology_codi/codi_down_sum.clif)


(cl-comment '1: Sum-T1')


(forall (x y z)
	(if
		(Cont z (sum x y))
		(exists (v)
			(or
				(and
					(P v z)
					(Cont v x) 
				)
				(and
					(P v z)
					(Cont v y)
				)
			)
		)
	)
)


(cl-comment '2: Sum-T2a')

(forall (x)
	(if
		(not (ZEX x))
		(P x (sum x x))
	)
)


(cl-comment '3: Sum-T2b')


(forall (x)
	(if
		(and
			(not (ZEX (sum x x)))
			(not (ZEX x))
		)
		(P (sum x x) x)
	)
)


(cl-comment '4: Sum-T3a: zero sum of zero')

(forall (x y)
	(if
		(and
			(ZEX x)
			(ZEX y)
		)
		(ZEX (sum x y))
	)
)

(cl-comment '5: Sum-T3b: zero sum of zero')

(forall (x y)
	(if
		(ZEX (sum x y))
		(ZEX x)
	)
)


(cl-comment '6: Sum-T4: sum with zero entity')

(forall (x y)
	(if 
		(ZEX y)
		(= (sum x y) x)
	)
)


(cl-comment '7: Sum-T5: dimension of the sum of equidimensional entities')

(forall (x y)
	(if
		(and
			(EqDim x y)
			(not (ZEX x))
			(not (ZEX y))
		)
		(EqDim x (sum x y))
	)
)




(cl-comment '8: Sum-T8a')

(forall (x y)
	(if	
		(and
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX (difference x y)))
			(P y x)
		)
		(P x (sum y (difference x y)))
	)
)


(cl-comment '9: Sum-T8b')

(forall (x y)
	(if
		(and
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX (difference x y)))
			(P y x)
		)
		(P (sum y (difference x y)) x)
	)
)



(cl-comment '10: Contributions to Sum-T9')

(forall (x y w)
	(if
		(and
			(EqDim x y)
			(Cont w (sum x y))
			(not (Cont w x))
			(not (Cont w y))
			(not (ZEX x))
			(not (ZEX y))
		)
		(Cont (intersection w x) x)
	)
)


(cl-comment '11: Contributions to Sum-T9')

(forall (x y w)
	(if
		(and
			(EqDim x y)
			(Cont w (sum x y))
			(not (Cont w x))
			(not (Cont w y))
			(not (ZEX x))
			(not (ZEX y))
		)
		(Cont (difference w x) y)
	)
)


(cl-comment '12: Contributions to Sum-T9')

(forall (x y w)
	(if
		(and
			(EqDim x y)
			(Cont w (sum x y))
			(not (Cont w x))
			(not (Cont w y))
			(not (ZEX x))
			(not (ZEX y))
		)
		(P (intersection w x) w)
	)
)


(cl-comment '13: Contributions to Sum-T9')

(forall (x y w)
	(if
		(and
			(EqDim x y)
			(Cont w (sum x y))
			(not (Cont w x))
			(not (Cont w y))
			(not (ZEX x))
			(not (ZEX y))
		)
		(P (difference w x) w)
	)
)



(cl-comment '14: Contributions to Sum-T9')

(forall (x y w)
	(if
		(and
			(EqDim x y)
			(Cont w (sum x y))
			(not (Cont w x))
			(not (Cont w y))
		)
		(not (PO (intersection w x) (difference w x)))
	)
)


(cl-comment '15: Sum-T6a')

(forall (x y z)
	(if
		(and
			(EqDim x y)
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX z))
			(PO z x)
		)
		(PO z (sum x y))
	)
)


(cl-comment '16: Sum-T6b')

(forall (x y z)
	(if
		(and
			(EqDim x y)
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX z))
			(PO z (sum x y))
		)
		(or
			(PO z x)
			(PO z y)
		)
	)
)


(cl-comment '17: Sum-T10a')

(forall (x y z)
	(if
		(and
			(EqDim x y)
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX z))
			(not (ZEX (sum x y)))
			(Cont x z)
			(Cont y z)
		)
		(Cont (sum x y) z)
	)
)


(cl-comment '18: Sum-T10b')

(forall (x y z)
	(if
		(and
			(EqDim x y)
			(not (ZEX x))
			(not (ZEX y))
			(Cont (sum x y) z)
			(not (ZEX (sum x y)))
		)
		(Cont x z)
	)
)


(cl-comment '19: Sum-T7a')

(forall (x y z w)
	(if
		(and
			(not (ZEX (sum (sum x y) z)))
			(not (ZEX (sum x y)))
			(not (ZEX (sum y z)))
			(not (ZEX (sum x (sum y z))))
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX z))
			(not (ZEX w))
			(P w (sum (sum x y) z))
		)
		(P w (sum x (sum y z)))
	)
)

(cl-comment '20: Sum-T7b')

(forall (x y z w)
	(if
		(and
			(not (ZEX (sum x (sum y z))))
			(not (ZEX (sum x y)))
			(not (ZEX (sum y z)))
			(not (ZEX (sum (sum x y) z)))
			(not (ZEX x))
			(not (ZEX y))
			(not (ZEX z))
			(not (ZEX w))
			(P w (sum x (sum y z)))
		)
		(P w (sum (sum x y) z))
	)
)


(cl-comment '21: Sum-T10a with Partial Overlap instead of Containment')

(forall (x y)
	(if
		(and
			(EqDim x y)
			(not (ZEX x))
			(not (ZEX y))
		)
		(forall (z)
			(if
				(and
					(PO x z)
					(PO y z)
				)
				(PO (sum x y) z)
			)
		)
	)
)


)