/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
*******************************************************************************/


(cl-text https://raw.githubusercontent.com/mereolog/computational_ontologies/main/TUpper/combined_time/hayes_vector_continuum.clif

(cl-imports https://raw.githubusercontent.com/mereolog/computational_ontologies/main/TUpper/timepoints/linear_point.clif)

(forall (i)
	(if	(timeinterval i) 
		(and	(timepoint (beginof i))
			(timepoint (endof i)))))

(forall (p q)
        (if     (and    (timepoint p)
                        (timepoint q))
                (and    (= p (beginof (between p q)))
                        (= q (endof (between p q))))))

(forall (i j)
(iff	(meets i j)
	(and	(timeinterval i)
		(timeinterval j)
		(= (endof i) (beginof j)))))

(forall (x y z)
	(if	(meets-at x y z) 
		(and 	(timeinterval x) 
			(timeinterval z) 
			(timepoint y))))
 
(forall (i p j)
	(iff	(meets-at i p j) 
		(and	(= p (endof i))
			(= p (beginof j))))) 

(forall (p q)
	(iff	(before p q) 
		(exists (i j k) 
			(and	(meets-at i p j)
				(meets-at j q k)))))

(forall (i j)
	(iff	(meets i j)
		(and	(timeinterval i)
			(timeinterval j)
			(= (endof i) (beginof j)))))

(forall (i j)
	(if	(meets i j)
		(= (plus i j) (between (beginof i) (endof j)))))

(forall (x) 
	(iff	(moment x) 
		(= (beginof x) (endof x))))

(forall (i)
	(iff	(forwards i)
		(before (beginof i) (endof i)))) 

(forall (i)
	(= (back i) (between (endof i)(beginof i))))

(forall (x) 
	(iff	(moment x) 
		(and	(timepoint x) 
			(timeinterval x)))) 


)
