% Saved by Prover9-Mace4 Version 0.5B, March 2008 (Dec 2007 LADR).

set(ignore_option_dependencies). % GUI handles dependencies

if(Prover9). % Options for Prover9
  assign(max_seconds, 60).
end_if.

if(Mace4).   % Options for Mace4
  assign(max_seconds, 60).
end_if.

formulas(assumptions).

%JEPD: Jonitly Exausitive Pairwise Disjoint
all x all y (b(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (a(x,y) 
-> (-b(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (d(x,y) 
-> (-a(x,y) & -b(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (di(x,y) 
-> (-a(x,y) & -d(x,y) & -b(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (o(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -b(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (oi(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -b(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (m(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -b(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (mi(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -b(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (s(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -b(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (si(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -b(x,y) & -e(x,y) & -ei(x,y) & -(x=y))).

all x all y (e(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -b(x,y) & -ei(x,y) & -(x=y))).

all x all y (ei(x,y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -b(x,y) & -(x=y))).

all x all y ((x=y) 
-> (-a(x,y) & -d(x,y) & -di(x,y) & -o(x,y) & -oi(x,y) 
& -m(x,y) & -mi(x,y) & -s(x,y) & -si(x,y) & -e(x,y) & -ei(x,y) & -b(x,y))).

%for each pair of x and y, the relation has to be one of the 13 relations
all x all y (b(x,y) | a(x,y) | d(x,y) | di(x,y) | o(x,y) | oi(x,y) | m(x,y) 
| mi(x,y) | s(x,y) | si(x,y) | e(x,y) | ei(x,y) | (x=y)).

%transitivity table axioms
%1st row - before
all x all y all z ((b(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((b(x,y) & a(y,z)) 
-> 
(b(x,z) | a(x,z) | d(x,z) | di(x,z) | o(x,z) | oi(x,z) | m(x,z) | mi(x,z) | s(x,z) | si(x,z) | e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((b(x,y) & d(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | d(x,z) | s(x,z) )).
all x all y all z ((b(x,y) & di(y,z)) -> b(x,z)).
all x all y all z ((b(x,y) & o(y,z)) -> b(x,z)).
all x all y all z ((b(x,y) & oi(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | d(x,z) | s(x,z) )).
all x all y all z ((b(x,y) & m(y,z)) -> b(x,z)).
all x all y all z ((b(x,y) & mi(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | d(x,z) | s(x,z) )).
all x all y all z ((b(x,y) & s(y,z)) -> b(x,z)).
all x all y all z ((b(x,y) & si(y,z)) -> b(x,z)).
all x all y all z ((b(x,y) & e(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | d(x,z) | s(x,z) )).
all x all y all z ((b(x,y) & ei(y,z)) -> b(x,z)).

%2nd row - after
all x all y all z ((a(x,y) & b(y,z))
->
(b(x,z) | a(x,z) | d(x,z) | di(x,z) | o(x,z) | oi(x,z) | m(x,z) | mi(x,z) | s(x,z) | si(x,z) | e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((a(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((a(x,y) & d(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((a(x,y) & di(y,z)) -> a(x,z)).
all x all y all z ((a(x,y) & o(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((a(x,y) & oi(y,z)) -> a(x,z)).
all x all y all z ((a(x,y) & m(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((a(x,y) & mi(y,z)) -> a(x,z)).
all x all y all z ((a(x,y) & s(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((a(x,y) & si(y,z)) -> a(x,z)).
all x all y all z ((a(x,y) & e(y,z)) -> a(x,z)).
all x all y all z ((a(x,y) & ei(y,z)) -> a(x,z)).

%3rd row - during
all x all y all z ((d(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((d(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((d(x,y) & d(y,z)) -> d(x,z)).
all x all y all z ((d(x,y) & di(y,z))
->
(b(x,z) | a(x,z) | d(x,z) | di(x,z) | o(x,z) | oi(x,z) | m(x,z) | mi(x,z) | s(x,z) | si(x,z) | e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((d(x,y) & o(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | d(x,z) | s(x,z))).
all x all y all z ((d(x,y) & oi(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((d(x,y) & m(y,z)) -> b(x,z)).
all x all y all z ((d(x,y) & mi(y,z)) -> a(x,z)).
all x all y all z ((d(x,y) & s(y,z)) -> d(x,z)).
all x all y all z ((d(x,y) & si(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((d(x,y) & e(y,z)) -> d(x,z)).
all x all y all z ((d(x,y) & ei(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | d(x,z) | s(x,z))).

%4th row - contains
all x all y all z ((di(x,y) & b(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((di(x,y) & a(y,z)) -> (a(x,z) | oi(x,z) | di(x,z) | mi(x,z) | si(x,z))).
all x all y all z ((di(x,y) & d(y,z)) -> (o(x,z) | oi(x,z) | d(x,z) | di(x,z) | s(x,z) | si(x,z) | e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((di(x,y) & di(y,z)) -> di(x,z)).
all x all y all z ((di(x,y) & o(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((di(x,y) & oi(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).
all x all y all z ((di(x,y) & m(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((di(x,y) & mi(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).
all x all y all z ((di(x,y) & s(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((di(x,y) & si(y,z)) -> di(x,z)).
all x all y all z ((di(x,y) & e(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).
all x all y all z ((di(x,y) & ei(y,z)) -> di(x,z)).

%5th row - overlaps
all x all y all z ((o(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((o(x,y) & a(y,z)) -> (a(x,z) | oi(x,z) | di(x,z) | mi(x,z) | si(x,z))).
all x all y all z ((o(x,y) & d(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((o(x,y) & di(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((o(x,y) & o(y,z)) -> (b(x,z) | o(x,z) | m(x,z))).
all x all y all z ((o(x,y) & oi(y,z)) -> (o(x,z) | oi(x,z) | d(x,z) | di(x,z) | s(x,z) | si(x,z) | e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((o(x,y) & m(y,z)) -> b(x,z)).
all x all y all z ((o(x,y) & mi(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).
all x all y all z ((o(x,y) & s(y,z)) -> o(x,z)).
all x all y all z ((o(x,y) & si(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((o(x,y) & e(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((o(x,y) & ei(y,z)) -> (b(x,z) | o(x,z) | m(x,z))).

%6th row - overlapped by
all x all y all z ((oi(x,y) & b(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((oi(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((oi(x,y) & d(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((oi(x,y) & di(y,z)) -> (a(x,z) | oi(x,z) | di(x,z) | mi(x,z) | si(x,z))).
all x all y all z ((oi(x,y) & o(y,z)) -> (o(x,z) | oi(x,z) | d(x,z) | di(x,z) | s(x,z) | si(x,z) | e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((oi(x,y) & oi(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z))).
all x all y all z ((oi(x,y) & m(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((oi(x,y) & mi(y,z)) -> a(x,z)).
all x all y all z ((oi(x,y) & s(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((oi(x,y) & si(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z))).
all x all y all z ((oi(x,y) & e(y,z)) -> oi(x,z)).
all x all y all z ((oi(x,y) & ei(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).

%7th row - meets
all x all y all z ((m(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((m(x,y) & a(y,z)) -> (a(x,z) | oi(x,z) | di(x,z) | mi(x,z) | si(x,z))).
all x all y all z ((m(x,y) & d(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((m(x,y) & di(y,z)) -> b(x,z)).
all x all y all z ((m(x,y) & o(y,z)) -> b(x,z)).
all x all y all z ((m(x,y) & oi(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((m(x,y) & m(y,z)) -> b(x,z)).
all x all y all z ((m(x,y) & mi(y,z)) -> (e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((m(x,y) & s(y,z)) -> m(x,z)).
all x all y all z ((m(x,y) & si(y,z)) -> m(x,z)).
all x all y all z ((m(x,y) & e(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((m(x,y) & ei(y,z)) -> b(x,z)).

%8th row - met by
all x all y all z ((mi(x,y) & b(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((mi(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((mi(x,y) & d(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((mi(x,y) & di(y,z)) -> a(x,z)).
all x all y all z ((mi(x,y) & o(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((mi(x,y) & oi(y,z)) -> a(x,z)).
all x all y all z ((mi(x,y) & m(y,z)) -> (s(x,z) | si(x,z) | (x=z))).
all x all y all z ((mi(x,y) & mi(y,z)) -> a(x,z)).
all x all y all z ((mi(x,y) & s(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((mi(x,y) & si(y,z)) -> a(x,z)).
all x all y all z ((mi(x,y) & e(y,z)) -> mi(x,z)).
all x all y all z ((mi(x,y) & ei(y,z)) -> mi(x,z)).

%9th row - starts
all x all y all z ((s(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((s(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((s(x,y) & d(y,z)) -> d(x,z)).
all x all y all z ((s(x,y) & di(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((s(x,y) & o(y,z)) -> (b(x,z) | o(x,z) | m(x,z))).
all x all y all z ((s(x,y) & oi(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((s(x,y) & m(y,z)) -> b(x,z)).
all x all y all z ((s(x,y) & mi(y,z)) -> mi(x,z)).
all x all y all z ((s(x,y) & s(y,z)) -> s(x,z)).
all x all y all z ((s(x,y) & si(y,z)) -> (s(x,z) | si(x,z) | (x=z))).
all x all y all z ((s(x,y) & e(y,z)) -> d(x,z)).
all x all y all z ((s(x,y) & ei(y,z)) -> (b(x,z) | o(x,z) | m(x,z))).

%10th row - started by
all x all y all z ((si(x,y) & b(y,z)) -> (b(x,z) | o(x,z) | m(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((si(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((si(x,y) & d(y,z)) -> (oi(x,z) | d(x,z) | e(x,z))).
all x all y all z ((si(x,y) & di(y,z)) -> di(x,z)).
all x all y all z ((si(x,y) & o(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((si(x,y) & oi(y,z)) -> oi(x,z)).
all x all y all z ((si(x,y) & m(y,z)) -> (o(x,z) | di(x,z) | ei(x,z))).
all x all y all z ((si(x,y) & mi(y,z)) -> mi(x,z)).
all x all y all z ((si(x,y) & s(y,z)) -> (s(x,z) | si(x,z) | (x=z))).
all x all y all z ((si(x,y) & si(y,z)) -> si(x,z)).
all x all y all z ((si(x,y) & e(y,z)) -> oi(x,z)).
all x all y all z ((si(x,y) & ei(y,z)) -> di(x,z)).

%11th row - ends
all x all y all z ((e(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((e(x,y) & a(y,z)) -> a(x,z)).
all x all y all z ((e(x,y) & d(y,z)) -> d(x,z)).
all x all y all z ((e(x,y) & di(y,z)) -> (a(x,z) | oi(x,z) | di(x,z) | mi(x,z) | si(x,z))).
all x all y all z ((e(x,y) & o(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((e(x,y) & oi(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z))).
all x all y all z ((e(x,y) & m(y,z)) -> m(x,z)).
all x all y all z ((e(x,y) & mi(y,z)) -> a(x,z)).
all x all y all z ((e(x,y) & s(y,z)) -> d(x,z)).
all x all y all z ((e(x,y) & si(y,z)) -> (a(x,z) | oi(x,z) | mi(x,z))).
all x all y all z ((e(x,y) & e(y,z)) -> e(x,z)).
all x all y all z ((e(x,y) & ei(y,z)) -> (e(x,z) | ei(x,z) | (x=z))).

%12h row - ended by
all x all y all z ((ei(x,y) & b(y,z)) -> b(x,z)).
all x all y all z ((ei(x,y) & a(y,z)) -> (a(x,z) | oi(x,z) | di(x,z) | mi(x,z) | si(x,z))).
all x all y all z ((ei(x,y) & d(y,z)) -> (o(x,z) | d(x,z) | s(x,z))).
all x all y all z ((ei(x,y) & di(y,z)) -> di(x,z)).
all x all y all z ((ei(x,y) & o(y,z)) -> o(x,z)).
all x all y all z ((ei(x,y) & oi(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).
all x all y all z ((ei(x,y) & m(y,z)) -> m(x,z)).
all x all y all z ((ei(x,y) & mi(y,z)) -> (oi(x,z) | di(x,z) | si(x,z))).
all x all y all z ((ei(x,y) & s(y,z)) -> o(x,z)).
all x all y all z ((ei(x,y) & si(y,z)) -> di(x,z)).
all x all y all z ((ei(x,y) & e(y,z)) -> (e(x,z) | ei(x,z) | (x=z))).
all x all y all z ((ei(x,y) & ei(y,z)) -> ei(x,z)).

all i all j (m(i,j) <-> meets(i,j)).

%extension
(all x all y
	(b(x,y)
	->
	(exists z
		(meets(x,z)
		& meets(z,y))))).

(all x all y
	(o(x,y)
	->
	(exists z
		(e(z,x)
		& s(z,y))))).

(all x all y
	(d(x,y)
	->
	(exists z
		(e(x,z)
		& s(z,y))))).

(all x all y
	(s(x,y)
	->
	(exists z
		(m(z,x)
		& m(z,y))))).

(all x all y
	(e(x,y)
	->
	(exists z
		(m(x,z)
		& m(y,z))))).

(all x all z
	(s(x,z)
	->
	(exists y
		(b(x,y)
		& m(z,y))))).

(all x all z
	(e(x,z)
	->
	(exists y
		(b(y,x)
		& m(y,z))))).

%pre definition
(all x all y
	(pre(x,y)
	<->
	(m(x,y) | b(x,y) | (x=y)))).

end_of_list.

formulas(goals).

(all x all y 
    (mi(x,y) 
    ->
	(exists z 
		(pre(z,x)
		& pre(z,y))))).

end_of_list.

