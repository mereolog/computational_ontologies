% Saved by Prover9-Mace4 Version 0.5B, March 2008 (Dec 2007 LADR).

set(ignore_option_dependencies). % GUI handles dependencies

if(Prover9). % Options for Prover9
  assign(max_seconds, 120).
end_if.

if(Mace4).   % Options for Mace4
  assign(max_seconds, 60).
end_if.

formulas(assumptions).

%interval meeting definition 
(all i all j (precedes(i,j) <-> exists k (meets(i,k) & meets(k,j)))).

(all i all j (starts(i,j) <-> exists k exists m exists n (meets(k,i) & meets(i,m)
			& meets(m,n)
			& meets(k,j)
			& meets(j,n)))).

(all i all j	(during(i,j) <-> exists k exists m exists n exists o (meets(k,m)
			& meets(m,i)
			& meets(i,n)
			& meets(n,o)
			& meets(k,j)
			& meets(j,o)))).

(all i all j (finishes(i,j) <-> exists k exists m exists n (meets(k,m)
			& meets(m,i)
			& meets(i,n)
			& meets(k,j)
			& meets(j,n)))).

%Finite interval meeting 
(all i all j
	(meets(i,j)
	->
	(timeinterval(i)
	& timeinterval(j)))).

(all i all j all k all m
	((meets(i,k)
	& meets(j,k)
	& meets(i,m))
	-> meets(j,m))).

(all i all j all k all l
	((meets(i,j)
	& meets(k,l))
	->
	(meets(i,l) | (exists n
		((meets(i,n) & meets(n,l))
		| (meets(k,n) & meets(n,j))))))).

(all i all j
	(meets(i,j)
	-> -meets(j,i))).

(all i all j all k all m
	((meets(i,j)
	& meets(j,k)
	& meets(k,m))
	->(exists n
		(meets(i,n)
		& meets(n,m))))).

%Trans def1
all x all y (precedence(x,y) <-> (precedes(x,y) | meets(x,y))).
%Trans def2
all x all y (inclusion(x,y) <-> (during(x,y)|starts(x,y) | finishes(x,y) | (x=y))).

%lemmas 3.1
all x all y all z ((during(x,y) & during(y,z))-> during(x,z)).
all x all y all z ((during(x,y) & starts(y,z))-> during(x,z)).
all x all y all z ((during(x,y) & finishes(y,z))-> during(x,z)).
%lemmas 3.2
all x all y all z ((starts(x,y) & starts(y,z))-> starts(x,z)).
all x all y all z ((starts(x,y) & finishes(y,z))-> during(x,z)).
all x all y all z ((starts(x,y) & during(y,z))-> during(x,z)).
%lemmas 3.3
all x all y all z ((finishes(x,y) & starts(y,z))-> during(x,z)).
all x all y all z ((finishes(x,y) & finishes(y,z))-> finishes(x,z)).
all x all y all z ((finishes(x,y) & during(y,z))-> during(x,z)).

%Lemmas 7.1
all x all y all z ((precedes(x,y) & during(z,y))->precedes(x,z)).
%Lemmas 7.2
all x all y all z ((precedes(x,y) & starts(z,y))->precedes(x,z)).
%Lemmas 7.3
all x all y all z ((precedes(x,y) & finishes(z,y))->precedes(x,z)).
%Lemmas 7.4
all x all y all z ((meets(x,y) & during(z,y))->precedes(x,z)).
%Lemmas 7.5
all x all y all z ((meets(x,y) & starts(z,y))->meets(x,z)).
%Lemmas 7.6
all x all y all z ((meets(x,y) & finishes(z,y))->precedes(x,z)).

end_of_list.

formulas(goals).

%Axiom 7
(all x all y all z 

	((precedence(x,y) 

	& inclusion(z,y)) 

	-> 

	precedence(x,z))).

end_of_list.

